plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

archivesBaseName = archive_base_name
version = "${mod_version}+Forge+${minecraft_version}"
group = "com.mrcrayfish.cgm.forge"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                cgm {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=${mod_id}.mixins.json"
            mods {
                cgm {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/')
            arg '-mixin.config=${mod_id}.mixins.json'
            mods {
                cgm {
                    source sourceSets.main
                }
            }
        }
    }
}

//processResources {
//    doLast {
//        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
//            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
//        }
//    }
//}

repositories {
    mavenLocal()
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("curse.maven:framework-549225:${framework_file_id}")
    compileOnly fg.deobf("curse.maven:configured-457570:${configured_file_id}") // Configured for 1.20 is not released
    implementation fg.deobf("curse.maven:catalogue-459701:${catalogue_file_id}")
    implementation fg.deobf("curse.maven:backpacked-352835:${backpacked_file_id}")
    compileOnly fg.deobf("curse.maven:simple-planes-388908:${simple_planes_file_id}") // Needs newer Forge
    implementation fg.deobf("curse.maven:recruits-523860:${recruits_file_id}")
    implementation fg.deobf("curse.maven:sophisticated-core-618298:${sop_core_file_id}")
    implementation fg.deobf("curse.maven:sophisticated-backpacks-422301:${sop_backpacks_file_id}")
    implementation fg.deobf("curse.maven:travelers-backpack-321117:${travelers_backpack_file_id}")
    compileOnly fg.deobf("curse.maven:controllable-317269:${controllable_file_id}")
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-forge-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-1.20.1-forge:${jei_version}")
    annotationProcessor('org.spongepowered:mixin:0.8.5:processor')

    // For compatibility test
    implementation fg.deobf("curse.maven:creativecore-257814:${creativecore_file_id}")
    implementation fg.deobf("curse.maven:cmdcam-251244:${cmdcam_file_id}")
}

jar {
    manifest {
        attributes([
            "Specification-Title": "Gun Mod",
            "Specification-Vendor": "MrCrayfish",
            "Specification-Version": "1",
            "Implementation-Title": "Gun Mod",
            "Implementation-Version": project.version,
            "Implementation-Vendor" : "MrCrayfish",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "${mod_id}.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${mod_id}-${minecraft_version}"
            version = mod_version
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        maven {
            name = "release"
            url = 'https://maven.nova-committee.cn/releases'
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}